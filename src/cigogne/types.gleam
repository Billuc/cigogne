import cigogne/internal/utils
import gleam/io
import gleam/list
import gleam/otp/actor
import gleam/time/calendar
import gleam/time/timestamp
import pog

/// The configuration linked to the database connection
pub type ConnectionConfig {
  /// The default configuration. It uses the DATABASE_URL envvar to connect
  EnvVarConfig
  /// A configuration using the URL to the database
  UrlConfig(url: String)
  /// A configuration using a pog.Config to connect. It disables schema generation.
  PogConfig(config: pog.Config)
  /// A configuration using a pog.Connection directly. It disables schema generation.
  ConnectionConfig(connection: pog.Connection)
}

/// cigogne's general configuration
pub type Config {
  Config(
    connection: ConnectionConfig,
    database_schema_to_use: String,
    migration_table_name: String,
    schema_config: SchemaConfig,
    migration_folder: String,
    migration_file_pattern: String,
  )
}

/// The configuration linked to schema file generation
pub type SchemaConfig {
  SchemaConfig(generate: Bool, filename: String)
}

/// The errors returned by cigogne
pub type MigrateError {
  EnvVarError(name: String)
  UrlError(url: String)
  FileError(path: String)
  CreateFolderError(path: String)
  PatternError(error: String)
  FileNameError(path: String)
  CompoundError(errors: List(MigrateError))
  ContentError(path: String, error: String)
  PGOTransactionError(error: pog.TransactionError(String))
  PGOQueryError(error: pog.QueryError)
  NoResultError
  SchemaQueryError(error: String)
  NoMigrationToApplyError
  NoMigrationToRollbackError
  MigrationNotFoundError(timestamp: timestamp.Timestamp, name: String)
  DateParseError(date: String)
  FileHashChanged(migration_ts: timestamp.Timestamp, migration_name: String)
  NameTooLongError(name: String)
  ActorStartError(error: actor.StartError)
}

/// Migrations are often generated by reading migration files.
/// However, we allow you to create your own Migrations
pub type Migration {
  Migration(
    path: String,
    timestamp: timestamp.Timestamp,
    name: String,
    queries_up: List(String),
    queries_down: List(String),
    sha256: String,
  )
}

/// Print a MigrateError to the stderr
pub fn print_migrate_error(error: MigrateError) -> Nil {
  case error {
    CompoundError(suberrors) -> {
      io.println_error("[")
      list.each(suberrors, print_migrate_error)
      io.println_error("]")
    }
    ContentError(path, message) ->
      io.println_error(
        "At [" <> path <> "]: Content wasn't right <" <> message <> ">",
      )
    EnvVarError(name) -> io.println_error("Couldn't find env var " <> name)
    FileError(path) ->
      io.println_error(
        "Couldn't access file at path ["
        <> path
        <> "]\nCheck the file or the parent folders exist and that you have the right permissions.",
      )
    FileNameError(path) ->
      io.println_error(
        "Migration filenames should have the format <MigrationNumber>-<MigrationName>.sql ! Got: ["
        <> path
        <> "]",
      )
    NoResultError ->
      io.println_error(
        "Got no result from DB (can't get last applied migration)",
      )
    PGOQueryError(suberror) ->
      io.println_error(utils.describe_query_error(suberror))
    PGOTransactionError(suberror) ->
      io.println_error(utils.describe_transaction_error(suberror))
    PatternError(message) -> io.println_error(message)
    UrlError(url) -> io.println_error("Database URL badly formatted: " <> url)
    SchemaQueryError(err) ->
      io.println_error("Error while querying schema : " <> err)
    NoMigrationToApplyError -> io.println_error("No migration to apply !")
    NoMigrationToRollbackError ->
      io.println_error("No user migration has been applied !")
    MigrationNotFoundError(ts, name) ->
      io.println_error(
        "Migration not found [timestamp: "
        <> ts |> timestamp.to_rfc3339(calendar.utc_offset)
        <> ", name: "
        <> name
        <> "]",
      )
    DateParseError(date) ->
      io.println_error("Date couldn't be properly parsed: " <> date)
    FileHashChanged(migration_ts, migration_name) ->
      io.println_error(
        "File contents of migration have changed since last applied [timestamp: "
        <> migration_ts |> timestamp.to_rfc3339(calendar.utc_offset)
        <> ", name: "
        <> migration_name
        <> "]",
      )
    NameTooLongError(name) ->
      io.println_error(
        "Migration name should be no more than 255 characters: " <> name,
      )
    CreateFolderError(path) ->
      io.println_error("Couldn't create folder at path [" <> path <> "]")
    ActorStartError(error:) ->
      case error {
        actor.InitExited(_) ->
          io.println_error("Actor init exited unexpectedly")
        actor.InitFailed(reason) ->
          io.println_error("Actor init failed: " <> reason)
        actor.InitTimeout -> io.println_error("Connection actor init timed out")
      }
  }
}
